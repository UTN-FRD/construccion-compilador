use std::str::FromStr;
use crate::ast::{Expr, Atom};

grammar;

pub Program: Vec<Expr> = {
    Expr*
}

// these are the rules we should follow
// https://docs.racket-lang.org/guide/syntax-overview.html#%28part._local-binding-intro%29
pub Expr: Expr = {
    "(" "define" "(" <fn_name: Id> <arg_names: Id*> ")" <body: Expr+> ")" => {
        Expr::DefineFunction(fn_name, arg_names, body)
    },

    "(" "define" <var_name: Id> <var_value: Expr> ")" => {
        Expr::DefineVariable(var_name, Box::new(var_value))
    },

    "(" "if" <cond: Expr> <positive: Expr> <negative: Expr?> ")" => {
        Expr::If(Box::new(cond), Box::new(positive), Box::new(negative))
    },

    "(" <l:List> ")" => Expr::List(l),
    <a:Atom> => Expr::Atom(a),
};

pub List: Vec<Expr> = {
    Expr*
}

pub Atom: Atom = {
    <s:Op> => Atom::Id(s),
    //TODO strings!
    //TODO float
    <s:Number> => Atom::Int(s),
    <s:Id> => Atom::Id(s),
}

Op: String = <s:r"[+\-*/=><]"> => s.to_string();
Id: String = <s:r"[[:alpha:]][\d\w\-_]*\??"> => s.to_string();
Number: i64 =  <s:r"[0-9]+"> => i64::from_str(s).unwrap();
